# GAP Installer for Windows
#
# Script written by Alexander Konovalov 
#
# Based on the previous NSIS scripts by A.Konovalov for GAP 4.4, 4.5, 4.6
# 
# Updated to use NSISModern User Interface using example scripts 
# from "NSIS\Examples\Modern UI" of NSIS 2.46 by Joost Verburg
#

#######################################################################
#
# include headers
#
!include "WordFunc.nsh"
!insertmacro WordReplace
!include Sections.nsh

# Include Modern UI

  !include "MUI2.nsh"

#######################################################################
#
# Declaring user variables - 
#
var GAP_VER      # GAP version in format 4.11.0
var INSTALL_PATH # Install path in the form C:\gap-4.11.0
var GAP_BAT      # to write gap.bat file
var GAPCMD_BAT   # to write gapcmd.bat file
var IndependentSectionState
var StartMenuFolder

#######################################################################
#
# User variables and other general settings: adjust them here as needed
# 
Section
StrCpy $GAP_VER "4.11.0"
SectionEnd

#Name and file
Name "GAP 4.11.0"
OutFile "gap-4.11.0.exe"

#Default installation folder
InstallDir "C:\gap-4.11.0"

#######################################################################

# A user without admin privileges should be able to install GAP
RequestExecutionLevel user

# Set compressing method (for test compiling may be commented out)
# and /SOLID can be removed (The best ratio is with /SOLID lzma,
# but it takes several times more to pack it, so we may be happy
# with the default compressor)
# SetCompressor /SOLID lzma
# SetCompressor lzma
# SetCompressor /SOLID zlib
# SetCompressor bzip2

#######################################################################
# Interface Settings

  !define MUI_ABORTWARNING

#######################################################################
# Pages

  !insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "copyright.txt"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY

  ;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU" 
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\GAP" 
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
  
  !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder

  !insertmacro MUI_PAGE_INSTFILES
  !insertmacro MUI_PAGE_FINISH

  !insertmacro MUI_UNPAGE_WELCOME
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  !insertmacro MUI_UNPAGE_FINISH

#######################################################################
# Languages

  !insertmacro MUI_LANGUAGE "English"

#######################################################################
#
# Installer Sections
#

#######################################################################
#
# The core GAP system - required component
#
Section "Core GAP system" SecGAPcore

  SectionIn RO

  # Set output path to the installation directory
  SetOutPath $INSTDIR
  # Put files there
  File gap-4.11.0\*.*

  SetOutPath $INSTDIR\.libs
  File /r gap-4.11.0\.libs\*.*

  SetOutPath $INSTDIR\autom4te.cache
  File /r gap-4.11.0\autom4te.cache\*.*

  SetOutPath $INSTDIR\bin
  File /r gap-4.11.0\bin\*.*
  File gapicon.ico

  SetOutPath $INSTDIR\cnf
  File /r gap-4.11.0\cnf\*.*

  SetOutPath $INSTDIR\doc
  File /r gap-4.11.0\doc\*.*

  SetOutPath $INSTDIR\etc
  File /r gap-4.11.0\etc\*.*

  SetOutPath $INSTDIR\extern
  File /r gap-4.11.0\extern\*.*

  SetOutPath $INSTDIR\gen
  File /r gap-4.11.0\gen\*.*

  SetOutPath $INSTDIR\grp
  File /r gap-4.11.0\grp\*.*

  SetOutPath $INSTDIR\hpcgap
  File /r gap-4.11.0\hpcgap\*.*

  SetOutPath $INSTDIR\lib
  File /r gap-4.11.0\lib\*.*

  SetOutPath $INSTDIR\obj
  File /r gap-4.11.0\obj\*.*

  SetOutPath $INSTDIR\src
  File /r gap-4.11.0\src\*.*

  SetOutPath $INSTDIR\terminfo
  File /r gap-4.11.0\terminfo\*.*

  SetOutPath $INSTDIR\tst
  File /r gap-4.11.0\tst\*.*

  # restore initial output path
  SetOutPath $INSTDIR 

  # Store installation folder
  WriteRegStr HKCU "Software\GAP" "" $INSTDIR

  # Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  # rewriting install path in format /cygdrive/c/gap-4.11.0
  StrCpy $INSTALL_PATH $INSTDIR
  ${WordReplace} $INSTALL_PATH ":" ""  "+" $INSTALL_PATH
  ${WordReplace} $INSTALL_PATH "\" "/" "+" $INSTALL_PATH

  # Write gap.bat file as follows:
  # set TERMINFO=/proc/cygdrive/c/gap-4.11.0/terminfo
  # set CYGWIN=nodosfilewarning
  # set LANG=en_US.UTF-8
  # set HOME=%HOMEDRIVE%%HOMEPATH%
  # set PATH=C:\gap-4.11.0\bin\x86_64-pc-cygwin-default64-kv7;%PATH%
  # cd %HOME%
  # start "GAP" C:\gap-4.11.0\bin\x86_64-pc-cygwin-default64-kv7\mintty.exe -s 120,40 /proc/cygdrive/C/gap-4.11.0/bin/gap.exe -l /proc/cygdrive/C/gap-4.11.0 %*
  # if NOT ["%errorlevel%"]==["0"] timeout 15
  # exit

  FileOpen $GAP_BAT $INSTDIR\bin\gap.bat w

  # set TERMINFO=/proc/cygdrive/c/gap-4.11.0/terminfo
  FileWrite $GAP_BAT "set TERMINFO=/proc/cygdrive/"
  FileWrite $GAP_BAT $INSTALL_PATH
  FileWrite $GAP_BAT "/terminfo"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # set CYGWIN=nodosfilewarning
  FileWrite $GAP_BAT "set CYGWIN=nodosfilewarning"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # set LANG=en_US.UTF-8
  FileWrite $GAP_BAT "set LANG=en_US.UTF-8"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # set HOME=%HOMEDRIVE%%HOMEPATH%
  FileWrite $GAP_BAT "set HOME=%HOMEDRIVE%%HOMEPATH%"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # set PATH=C:\gap-4.11.0\bin\x86_64-pc-cygwin-default64-kv7;%PATH%
  FileWrite $GAP_BAT "set PATH="
  FileWrite $GAP_BAT $INSTDIR
  FileWrite $GAP_BAT "\bin\x86_64-pc-cygwin-default64-kv7;%PATH%"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # cd %HOME%
  FileWrite $GAP_BAT "cd %HOME%"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # start "GAP" C:\gap-4.11.0\bin\x86_64-pc-cygwin-default64-kv7\mintty.exe -s 120,40 /proc/cygdrive/C/gap-4.11.0/gap.exe -l /proc/cygdrive/C/gap-4.11.0 %*
  FileWrite $GAP_BAT "start $\"GAP$\" " 
  FileWrite $GAP_BAT $INSTDIR
  FileWrite $GAP_BAT "\bin\x86_64-pc-cygwin-default64-kv7\mintty.exe -s 120,40 /proc/cygdrive/"
  FileWrite $GAP_BAT $INSTALL_PATH
  FileWrite $GAP_BAT "/gap.exe -l /proc/cygdrive/"
  FileWrite $GAP_BAT $INSTALL_PATH
  FileWrite $GAP_BAT " %*"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # if NOT ["%errorlevel%"]==["0"] timeout 15
  FileWrite $GAP_BAT "if NOT [$\"%errorlevel%$\"]==[$\"0$\"] timeout 15"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  # exit
  FileWrite $GAP_BAT "exit"
    FileWriteByte $GAP_BAT "13"
    FileWriteByte $GAP_BAT "10"

  FileClose $GAP_BAT


  # Write gapcmd.bat file as follows:
  # set TERMINFO=/proc/cygdrive/c/gap-4.11.0/terminfo
  # set CYGWIN=nodosfilewarning
  # set LANG=en_US.UTF-8
  # set HOME=%HOMEDRIVE%%HOMEPATH%
  # set PATH=C:\gap-4.11.0\bin\x86_64-pc-cygwin-default64-kv7;%PATH%
  # cd %HOME%
  # C:\gap-4.11.0\gap.exe -l /proc/cygdrive/C/gap-4.11.0 %*
  # if NOT ["%errorlevel%"]==["0"] timeout 15
  # exit

  FileOpen $GAPCMD_BAT $INSTDIR\bin\gapcmd.bat w

  # set TERMINFO=/proc/cygdrive/c/gap-4.11.0/terminfo
  FileWrite $GAPCMD_BAT "set TERMINFO=/proc/cygdrive/"
  FileWrite $GAPCMD_BAT $INSTALL_PATH
  FileWrite $GAPCMD_BAT "/terminfo"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"

  # set CYGWIN=nodosfilewarning
  FileWrite $GAPCMD_BAT "set CYGWIN=nodosfilewarning"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"

  # set LANG=en_US.UTF-8
  FileWrite $GAPCMD_BAT "set LANG=en_US.UTF-8"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"   

  # set HOME=%HOMEDRIVE%%HOMEPATH%
  FileWrite $GAPCMD_BAT "set HOME=%HOMEDRIVE%%HOMEPATH%"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"

  # set PATH=C:\gap-4.11.0\bin\x86_64-pc-cygwin-default64-kv7;%PATH%
  FileWrite $GAPCMD_BAT "set PATH="
  FileWrite $GAPCMD_BAT $INSTDIR
  FileWrite $GAPCMD_BAT "\bin\x86_64-pc-cygwin-default64-kv7;%PATH%"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"

  # cd %HOME%
  FileWrite $GAPCMD_BAT "cd %HOME%"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"

  # C:\gap-4.11.0\gap.exe -l /proc/cygdrive/C/gap-4.11.0 %*
  FileWrite $GAPCMD_BAT $INSTDIR
  FileWrite $GAPCMD_BAT "\gap.exe -l /proc/cygdrive/"
  FileWrite $GAPCMD_BAT $INSTALL_PATH 
  FileWrite $GAPCMD_BAT " %*"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"


  # if NOT ["%errorlevel%"]==["0"] timeout 15
  FileWrite $GAPCMD_BAT "if NOT [$\"%errorlevel%$\"]==[$\"0$\"] timeout 15"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"

  # exit
  FileWrite $GAPCMD_BAT "exit"
    FileWriteByte $GAPCMD_BAT "13"
    FileWriteByte $GAPCMD_BAT "10"
  FileClose $GAPCMD_BAT


  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    
  CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\GAP $GAP_VER.lnk" "$INSTDIR\bin\gap.bat" "" "$INSTDIR\bin\gapicon.ico" 0
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\GAP Tutorial.lnk" "$INSTDIR\doc\tut\chap0.html" "" "$INSTDIR\doc\tut\chap0.html" 0
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\GAP Reference Manual.lnk" "$INSTDIR\doc\ref\chap0.html" "" "$INSTDIR\doc\ref\chap0.html" 0
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall GAP $GAP_VER.lnk" "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_END

SectionEnd

